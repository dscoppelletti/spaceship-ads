
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
//    id 'org.jetbrains.dokka'
    id 'it.scoppelletti.spaceship.android-library'
}

group 'it.scoppelletti.spaceship'
version '1.0.0'
description 'Spaceship for AdMob'
archivesBaseName = 'spaceship-ads'

android {
    compileSdkVersion sdk_target
    defaultConfig {
        minSdkVersion sdk_min
        targetSdkVersion sdk_target
        vectorDrawables.useSupportLibrary = true
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'consumer-rules.pro'
            debuggable false
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

spaceship {
    url = 'http://github.com/dscoppelletti/spaceship-ads'
    scmUrl = 'git@github.com:dscoppelletti/spaceship-ads.git'
    inceptionYear = '2018'
}

configurations {
    bom
    api.extendsFrom(bom)
    implementation.extendsFrom(bom)
    kapt.extendsFrom(bom)
    coreLibraryDesugaring.extendsFrom(bom)
}

dependencies {
    bom platform(BOM)
    bom platform("com.google.firebase:firebase-bom:${firebase_version}")
    api 'androidx.activity:activity-ktx'
    api 'androidx.annotation:annotation'
    api 'androidx.appcompat:appcompat'
    api 'androidx.browser:browser'
    api 'androidx.constraintlayout:constraintlayout'
    api 'androidx.fragment:fragment-ktx'
    implementation 'androidx.lifecycle:lifecycle-common-java8'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx'
    implementation 'androidx.navigation:navigation-fragment-ktx'
    implementation 'androidx.navigation:navigation-ui-ktx'
    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection'
    api 'com.google.android.gms:play-services-ads'
    api 'com.google.android.material:material'
    api 'com.google.dagger:dagger'
    kapt 'com.google.dagger:dagger-compiler'
    api 'com.google.firebase:firebase-core'
    api 'com.squareup.moshi:moshi'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen'
    api 'com.squareup.okhttp3:okhttp'
    api 'com.squareup.retrofit2:retrofit'
    api 'it.scoppelletti.spaceship:spaceship'
    api 'it.scoppelletti.spaceship:spaceship-html'
    api 'it.scoppelletti.spaceship:spaceship-http'
    api 'it.scoppelletti.spaceship:spaceship-preference'
    api 'it.scoppelletti.spaceship:spaceship-stdlib'
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
    implementation 'com.squareup.okio:okio'
    implementation 'com.squareup.retrofit2:converter-moshi'
    implementation 'io.github.microutils:kotlin-logging'
    implementation 'org.slf4j:slf4j-api'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
    testImplementation 'org.slf4j:slf4j-simple'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs'
}
